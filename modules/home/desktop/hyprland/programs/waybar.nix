{
  config,
  lib,
  pkgs,
  ...
}@args:

with lib;
let
  variables = import ../variables.nix;
in
{
  stylix.targets.waybar.addCss = false;
  home.packages = with pkgs; [ playerctl ]; # required by mpris

  programs.waybar = {
    enable = true;

    systemd = {
      enable = false;
      target = "graphical-session.target";
    };

    settings = {
      mainBar = {
        layer = "top";
        position = "top";
        mode = "dock";
        exclusive = true;
        passthrough = false;
        gtk-layer-shell = true;
        ipc = true;
        fixed-center = true;
        height = variables.topBar.height;
        # Use same values as windows for consistence
        margin-top = variables.window.gap;
        margin-left = variables.window.gap;
        margin-right = variables.window.gap;
        margin-bottom = 0; # Use gaps_out configured in hyprland

        modules-left = [
          "hyprland/workspaces"
        ];

        modules-center = [
          "mpris"
          "clock"
        ];

        modules-right = [
          "power-profiles-daemon"
          "idle_inhibitor"
          "pulseaudio"
          "network"
          "bluetooth"
          "privacy"
          "battery"
          "tray"
          "group/group_power"
        ];

        mpris = {
          format = "{player_icon} {title} - {artist}";
          format-paused = "{status_icon} <i>{title} - {artist}</i>";
          player-icons = {
            default = "󰐊";
          };
          status-icons = {
            paused = "󰏤";
            playing = "󰐊";
          };
          max-length = 30;
        };

        power-profiles-daemon = {
          format = "{icon}";
          tooltip-format = "Power profile: {profile}\nDriver: {driver}";
          tooltip = true;
          format-icons = {
            default = "󰗖";
            performance = "󰓅";
            balanced = "󰾅";
            power-saver = "󰌪";
          };
        };

        idle_inhibitor = {
          format = "{icon}";
          format-icons = {
            activated = "󰒳";
            deactivated = "󰒲";
          };
        };

        clock = {
          format = "{:L%A %d %b, %H:%M}";
          tooltip-format = "<big>{:L%Y %B}</big>\n<tt><small>{calendar}</small></tt>";
        };

        tray = {
          spacing = variables.topBar.gap;
          # See https://github.com/nix-community/stylix/blob/c4b8e80a1020e09a1f081ad0f98ce804a6e85acf/modules/waybar/hm.nix#L66
          icon-size = config.stylix.fonts.sizes.desktop;
        };

        pulseaudio =
          let
            volumes = import ../scripts/volume.nix args;
          in
          {
            on-click = getExe pkgs.pavucontrol;
            format = "{icon}";
            format-muted = "󰝟";
            format-icons.default = [
              "󰖁"
              "󰕿"
              "󰖀"
              "󰕾"
            ];
            on-click-middle = volumes.toggleMute;
            on-scroll-up = volumes.raiseVolume;
            on-scroll-down = volumes.lowerVolume;
          };

        network = {
          on-click = "${getExe pkgs.kitty} --title nmtui ${getExe' pkgs.networkmanager "nmtui"}";
          format = "{ifname}";
          format-wifi = "󰖩";
          format-ethernet = "󰈀";
          format-disconnected = "󰌙";
          tooltip-format = "{ifname} via {gwaddr} 󰮂";
          tooltip-format-wifi = "<big>{essid}</big>\n<tt>{signalStrength}%</tt>";
          tooltip-format-ethernet = "<big>{ifname}</big>\n<tt>{ipaddr}/{cidr}</tt>";
          tooltip-format-disconnected = "Disconnected";
          max-length = 50;
        };

        bluetooth = {
          on-click = "${getExe pkgs.overskride}";
          format = "󰂯";
          format-off = "󰂲";
          format-on = "󰂯";
          format-connected = "󰂱";
          tooltip-format = "{controller_alias}\t{controller_address}\n\n{num_connections} connected";
          tooltip-format-connected = "{controller_alias}\t{controller_address}\n\n{num_connections} connected\n\n{device_enumerate}";
          tooltip-format-enumerate-connected = "{device_alias}\t{device_address}";
          tooltip-format-enumerate-connected-battery = "{device_alias}\t{device_address}\t{device_battery_percentage}%";
        };

        privacy = {
          icon-spacing = variables.topBar.gap;
          modules = [
            {
              type = "screenshare";
              tooltip = true;
            }
            {
              type = "audio-in";
              tooltip = true;
            }
          ];
        };

        battery = {
          interval = 60;
          format = "{icon}";
          tooltip-format = "{capacity}% • {power:.1f}W • {timeTo} • Health: {health}%";
          format-icons = {
            default = [
              "󰂃"
              "󰁺"
              "󰁻"
              "󰁼"
              "󰁽"
              "󰁾"
              "󰁿"
              "󰂀"
              "󰂁"
              "󰂂"
              "󰁹"
            ];
            charging = [
              "󰢟"
              "󰢜"
              "󰂆"
              "󰂇"
              "󰂈"
              "󰢝"
              "󰂉"
              "󰢞"
              "󰂊"
              "󰂋"
              "󰂅"
            ];
          };
        };

        "group/group_power" = {
          orientation = "inherit";
          drawer = {
            transition = 500;
            transition-left-to-right = false;
          };
          modules = [
            "custom/power"
            "custom/quit"
            "custom/lock"
            "custom/reboot"
          ];
        };

        "custom/quit" = {
          format = "󰍃";
          tooltip-format = "Se déconnecter";
          on-click = "hyprctl dispatch exit";
        };

        "custom/lock" = {
          format = "󰌾";
          tooltip-format = "Verrouiller la session";
          on-click = "hyprlock"; # Installed by programs.hyprlock
        };

        "custom/reboot" = {
          format = "󰜉";
          tooltip-format = "Redémarrer";
          on-click = "reboot";
        };

        "custom/power" = {
          format = "󰐥";
          tooltip-format = "Arrêter le système";
          on-click = "shutdown now";
        };
      };
    };

    style =
      let
        border.radius = toString variables.window.border.radius;
        workspace.radius = toString (variables.window.border.radius / 2);
        padding.x = toString (variables.window.gap * 2);
        modules.gap = toString (variables.topBar.gap / 2);
      in
      ''
        /* Custom section */
        * {
          margin: 0px;
          padding: 0px;
        }

        window#waybar {
          transition-property: background-color;
          transition-duration: 0.5s;
          background: transparent;
          color: @base05;
        }

        tooltip {
          border-radius: ${border.radius}px;
          border-color: @base0D;
        }

        tooltip label {
          color: @base05;
          margin-right: 5px;
          margin-left: 5px;
        }

        .modules-left,
        .modules-center,
        .modules-right {
          background: @base00;
          padding-right: ${padding.x}px;
          padding-left: ${padding.x}px;
          border-radius: ${border.radius}px;
        }

        /* Gap for all modules */
        #clock,
        #mpris,
        #pulseaudio,
        #network,
        #bluetooth,
        #privacy,
        #idle_inhibitor,
        #battery,
        #power-profiles-daemon,
        #tray,
        #custom-quit,
        #custom-lock,
        #custom-reboot,
        #custom-power {
          padding: 0 ${modules.gap}px;
        }

        #custom-power {
          color: @base08;
        }

        /* Workspaces */
        #workspaces button {
          margin: ${modules.gap}px 0;
          padding: 0 ${modules.gap}px;
          color: @base03;
          border: none;
          transition: all 0.2s ease;
          border-radius: ${workspace.radius}px;
        }

        #workspaces button.active,
        #workspaces button.focused {
          color: @base04;
          background: alpha(@base01, 0.6);
        }

        #workspaces button:hover {
          background: alpha(@base02, 0.4);
          color: @base05;
        }

        #workspaces button.urgent {
          background: @base08;
          color: @base00;
        }
      '';
  };
}
